plugins {
  id 'base'

  id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'

  id "com.github.ben-manes.versions" version "0.52.0"
  id "com.gorylenko.gradle-git-properties" version "2.5.2" apply false
}

allprojects {
  group = 'org.sitemesh'
  repositories { mavenCentral() }
}

subprojects {
  apply plugin: 'java-library'
  apply plugin: 'maven-publish'
  apply plugin: 'signing'
  apply plugin: 'com.gorylenko.gradle-git-properties'

  java {
    toolchain { languageVersion = JavaLanguageVersion.of(17) }
    withSourcesJar()
    withJavadocJar()
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java
        pom {
          name = project.name
          description = 'SiteMesh 3 â€“ HTML decoration and layout framework'
          url = 'https://github.com/sitemesh/sitemesh3'
          licenses {
            license {
              name = 'Apache License, Version 2.0'
              url = 'https://www.apache.org/licenses/LICENSE-2.0'
              distribution = 'repo'
            }
          }
          scm {
            url = 'https://github.com/sitemesh/sitemesh3'
            connection = 'scm:git:https://github.com/sitemesh/sitemesh3.git'
            developerConnection = 'scm:git:ssh://git@github.com/sitemesh/sitemesh3.git'
          }
          developers {
            developer {
              id = 'sitemesh'
              name = 'SiteMesh Contributors'
              url = 'https://github.com/sitemesh'
            }
          }
        }
      }
    }
  }

  signing {
    def key = findProperty('signing.key') as String
    def pass = findProperty('signing.password') as String
    if (key && pass) {
      useInMemoryPgpKeys(key, pass)
      sign publishing.publications
    }
  }
}

nexusPublishing {
  packageGroup.set('org.sitemesh')
  repositories {
    sonatype {
      nexusUrl.set(uri('https://ossrh-staging-api.central.sonatype.com/service/local/'))
      snapshotRepositoryUrl.set(uri('https://central.sonatype.com/repository/maven-snapshots/'))
      username.set(providers.gradleProperty('sonatypeUsername'))
      password.set(providers.gradleProperty('sonatypePassword'))
    }
  }
}
