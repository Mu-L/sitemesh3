apply plugin: 'com.gorylenko.gradle-git-properties'

gitProperties {
    customProperty 'sitemesh.version', { project.version }
}

configurations {
    lexer
}

sourceSets.main.java.srcDirs += ['build/generated-sources/jflex']

description = 'SiteMesh library'

dependencies {
    testImplementation 'junit:junit:4.5'
    testImplementation 'org.eclipse.jetty:jetty-servlet:11.0.15'
    testImplementation 'org.eclipse.jetty:jetty-util:11.0.15'
    testImplementation 'org.apache.ant:ant-testutil:1.10.14'

    compileOnly 'jakarta.servlet:jakarta.servlet-api:6.0.0'
    compileOnly 'org.apache.ant:ant:1.10.14'
    lexer 'de.jflex:jflex:1.4.3'
}


compileJava.doFirst {
    ant.taskdef classname: 'JFlex.anttask.JFlexTask', name: 'jflex', classpath: configurations.lexer.asPath
    ant.jflex(file: "src/main/java/org/sitemesh/tagprocessor/lexer.flex", destdir: "build/generated-sources/jflex")// "src/main/java/")
}
